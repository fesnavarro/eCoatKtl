FUNCTION "fGroup" : Void
TITLE = fConveyor
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : fesnavarro
FAMILY : Conveyor
NAME : '2002'
VERSION : 0.8
//Function to parameter of conveyor
   VAR_INPUT 
      location : Int;
   END_VAR

   VAR_IN_OUT 
      index : "tIndex";
   END_VAR

   VAR_TEMP 
      action : REF_TO "tAction";
      actual : REF_TO "tActual";
      interface : REF_TO "tInterface";
      i : Int;
      group : REF_TO "tEquipment";
      preHeating : REF_TO "tPreHeating";
   END_VAR


BEGIN
	#actual := REF("dbData".actual);
	#action := REF("dbData".action);
	#group := REF("dbArchiveGroup".group[#index.acc]);
	#preHeating := REF("dbArchiveParGroup".preHeating[#index.acc]);
	
	#index.pathParent := #index.acc;
	#index.acc := 0;
	#group^.selectedByInterface := 0;
	
	REGION interface selected
	    FOR #i := 1 TO 6 DO
	        IF #index.pathParent = ("dbInterface".interface[#i].equipamentId - #location) THEN
	            #group^.selectedByInterface := #i;
	            "dbInterface".interface[#i].controlId := 0;
	            "dbInterface".interface[#i].auxStatus[2].setSignal := TRUE;
	            "fStatus"("dbInterface".interface[#i].auxStatus[2]);
	        END_IF;
	    END_FOR;
	END_REGION
	
	REGION automatic or manual
	    REGION parameters
	        CASE #index.pathParent (*parent*)OF
	            1..5:
	                CASE #index.acc (*control*)OF
	                    0:
	                        //set
	                        #group^.autoMan.setDelayOfAuto := 50;
	                        #group^.autoMan.setDelayOfManual := 50;
	                        #group^.autoMan.setPulseSelAutoMan :=
	                        "dbInterface".interface[#group^.selectedByInterface].autoMan.setPulseSelAutoMan;
	                        #group^.autoMan.setSelAuto := TRUE;
	                        #group^.autoMan.setSelManual := TRUE;
	                        "dbInterface".interface[#group^.selectedByInterface].autoMan.getStsManual :=
	                        #group^.autoMan.getStsManual;
	                END_CASE;
	        END_CASE;
	    END_REGION
	    
	    REGION call function
	        "fAutoMan"(#group^.autoMan);
	    END_REGION
	END_REGION
	
	REGION automatic and running
	    REGION parameters
	        CASE #index.pathParent (*parent*)OF
	            2:
	                CASE #index.acc (*control*)OF
	                    0:
	                        //set
	                        "fPreHeating"(setDateTimeOn:=#preHeating^.startTime,
	                                      setDateTimeOff:=#preHeating^.stopTime,
	                                      start=>#preHeating^.start,
	                                      stop=>#preHeating^.stop,
	                                      getDateTime=>"dbUtilities".dateTime);
	                        #group^.autoRun.setFailureDetected :=
	                        #actual^.generalFailureDetected.signal;
	                        #group^.autoRun.setDelayTurnOff := 50;
	                        #group^.autoRun.setDelayTurnOn := 50;
	                        #group^.autoRun.setPulseSelAutoRun :=
	                        "dbInterface".interface[#group^.selectedByInterface].autoRun.setPulseSelAutoRun;
	                        #group^.autoRun.setSelTurnOn := #preHeating^.start;
	                        #group^.autoRun.setSelTurnOff := #preHeating^.stop;
	                        #group^.autoRun.setStsAuto :=
	                        #group^.autoMan.getStsAuto;
	                        "dbInterface".interface[#group^.selectedByInterface].autoRun.getStsAutoRun :=
	                        #group^.autoRun.getStsAutoRun;
	                END_CASE;
	            1,3..5:
	                CASE #index.acc (*control*)OF
	                    0:
	                        //set
	                        #group^.autoRun.setFailureDetected :=
	                        #actual^.generalFailureDetected.signal;
	                        #group^.autoRun.setDelayTurnOff := 50;
	                        #group^.autoRun.setDelayTurnOn := 50;
	                        #group^.autoRun.setPulseSelAutoRun :=
	                        "dbInterface".interface[#group^.selectedByInterface].autoRun.setPulseSelAutoRun;
	                        #group^.autoRun.setSelTurnOn := FALSE;
	                        #group^.autoRun.setSelTurnOff := FALSE;
	                        #group^.autoRun.setStsAuto :=
	                        #group^.autoMan.getStsAuto;
	                        "dbInterface".interface[#group^.selectedByInterface].autoRun.getStsAutoRun :=
	                        #group^.autoRun.getStsAutoRun;
	                END_CASE;
	        END_CASE;
	        
	    END_REGION
	    
	    REGION call function
	        "fAutoRun"(#group^.autoRun);
	    END_REGION
	END_REGION
END_FUNCTION

